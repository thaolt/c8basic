entrypoints Program ;

Prog.   	Program ::= [Stmt] ;


FArg.		Arg		::=	Id ; -- Function arguments
FPar.		Param	::=	Exp ; -- Function parameters

separator   Arg "," ;
separator   Param "," ;

-- Declarations
DLetTA.		Decl4	::= Id "as" Type "=" Exp ;
DLetT.		Decl4	::= Id "as" Type ;
DLetA.		Decl4	::= Id "=" Exp ;
DLetB.		Decl4	::= Id ;
DGlbl.		Decl3	::= "global" Decl4 ;
DLet.		Decl3	::= "let" Decl4 ;
DSubE.		Decl2	::= "endsub" ;
DSub.		Decl2	::= "sub" Id ;
DFnER.		Decl1	::= "return" Exp ;
DFnEN.		Decl1	::= "return" ;
DFunc.		Decl	::= "func" Id "(" [Arg] ")" ;
DStruct.	Decl	::= "struct" Id ;
DStructE.	Decl	::= "endstruct";
DLbl.		Decl	::= LLabel;

terminator Decl3 "" ;
coercions Decl 4 ;

-- Statements
SEval.		Stmt2 	::= Exp ;
SAsg. 		Stmt1	::= Id "=" Exp ;
SDecl.		Stmt 	::= Decl ;
SBra.		Stmt 	::= Branch ;
SLoop.		Stmt 	::= Loop ;
SGoto.		Stmt 	::= "goto" Id ;
SGoSb.		Stmt 	::= "gosub" Id ;
SAsm.		Stmt 	::= "_" String ;
SCall.		Stmt 	::= Id "(" [Param] ")" ;

coercions Stmt 2 ;

terminator Stmt ";" ;
separator Stmt "" ;

-- Loops
LForE.		Loop2	::= "endfor" ;
LForN.		Loop1	::= "step" Exp ;
LForO.		Loop1	::= ;
LFor.		Loop	::= "for" Stmt1 "to" Exp Loop1 ;
LWhile.		Loop	::= "while" Exp ; 
LWhileE.	Loop	::= "endwhile" ; 
LRepeat.	Loop	::= "repeat" ;
LUntil.		Loop	::= "until" Exp ;

coercions Loop 2 ;

-- Branching
BIfE.		Branch ::= "endif" ;
BIfEl.		Branch ::= "else" ;
BIfEI.		Branch ::= "elif" Exp "then" ;
BIf.		Branch  ::= "if" Exp "then" ;

-- coercions Branch 2 ;

-- Expressions
_.			Exp 	::= Exp11 ;
ELgOr.		Exp1  	::= Exp1 "or" Exp2;
ELgAnd.		Exp2  	::= Exp2 "and" Exp3;
EBitOr.		Exp3  	::= Exp3 "|" Exp4;
EBitXor.	Exp4  	::= Exp4 "^" Exp5;
EBitAnd.	Exp5  	::= Exp5 "&" Exp6;
ELgEq.		Exp6  	::= Exp6 "==" Exp7;
ELgNe.		Exp6  	::= Exp6 "!=" Exp7;
ELgLt.		Exp7 	::= Exp7 "<" Exp8;
ELgGt.		Exp7 	::= Exp7 ">" Exp8;
ELgLte.		Exp7 	::= Exp7 "<=" Exp8;
ELgGte.		Exp7 	::= Exp7 ">=" Exp8;
EBitShl.	Exp8 	::= Exp8 "<<" Exp9;
EBitShr.	Exp8 	::= Exp8 ">>" Exp9;
EAdd. 		Exp9  	::= Exp9 "+" Exp10 ;
ESub. 		Exp9  	::= Exp9  "-" Exp10 ;
EDiv. 		Exp10 	::= Exp10 "/" Exp11 ;
EMul. 		Exp10 	::= Exp10 "*" Exp11 ;
EMod. 		Exp10 	::= Exp10 "mod" Exp11 ;
EChr.		Exp11 	::= Char ;
EStr.		Exp11 	::= String ;
EVar. 		Exp11 	::= Id ;
ENum.		Exp11	::= Number ;
_.			Exp12 	::= "(" Exp ")" ;

coercions Exp 12 ;

-- Numbers
NDec.		Number 	::= NumDec ;
NBin.		Number 	::= NumBin ;
NHex.		Number 	::= NumHex ;


TByte.		Type1  	::= "byte" ;
TWord.		Type1	::= "word" ;
TDWord.		Type1	::= "dword" ;
TQWord.		Type1	::= "qword" ;
TDQWord.	Type1	::= "dqword" ;
TArr.		Type	::= Type1 "[" Number "]" ;
_.			Type	::= Type1 ;

coercions Type 1 ;

comment "//" ;
comment "/*" "*/" ;

token Id (letter (letter | digit | '_')*) ;
token LLabel (letter (letter | digit | '_')+ ':') ;
token NumBin ('%' ('0'|'1')+ ) ;
token NumDec ( ["123456789"] (digit)* ) ;
token NumHex ('$' (digit | ["ABCDEF"] | ["abcdef"])+ ) ;
token NumFloat ( ["123456789"] (digit)* (('.') (digit)+)? ) ;



